# METHOD_BODY = STATEMENT_LIST
# STATEMENT_LIST = STATEMENT ANOTHER_STATEMENT
# ANOTHER_STATEMENT = STATEMENT ANOTHER_STATEMENT | '\L'
# STATEMENT = DECLARATION | IF | WHILE | ASSIGNMENT
# DECLARATION = PRIMITIVE_TYPE 'id' ';'
# PRIMITIVE_TYPE = 'int' | 'float'
# IF = 'if' '(' EXPRESSION ')' '{' STATEMENT '}' 'else' '{' STATEMENT '}'
# WHILE = 'while' '(' EXPRESSION ')' '{' STATEMENT '}'
# ASSIGNMENT = 'id' 'assign' EXPRESSION ';'
# EXPRESSION = SIMPLE_EXPRESSION EXPRESSION2
# EXPRESSION2 = '\L' | 'relop' SIMPLE_EXPRESSION
# SIMPLE_EXPRESSION = TERM ANOTHER_EXPRESSION | SIGN TERM ANOTHER_EXPRESSION
# ANOTHER_EXPRESSION = 'addop' TERM ANOTHER_EXPRESSION | '\L'
# TERM = FACTOR ANOTHER_TERM
# ANOTHER_TERM = 'mulop' FACTOR ANOTHER_TERM | '\L'
# FACTOR = 'id' | 'num' | '(' EXPRESSION ')'
# SIGN = '+' | '-'
